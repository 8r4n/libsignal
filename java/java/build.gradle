plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    id "de.undercouch.download" version "4.1.1"
}

sourceCompatibility = 1.7
archivesBaseName = "signal-client-java"
version          = version_number
group            = group_info

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java/', '../tests/src/test/java']
        }
    }
    otherPlatformLibraries {}
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

test {
    testLogging {
        events 'passed'
        showStandardStreams = true
        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.register('testableJar', Jar) {
    from sourceSets.main.output
    from sourceSets.otherPlatformLibraries.resources
    classifier = 'testable'

    onlyIf { gradle.taskGraph.hasTask(":java:publish") }

    def extraResources = ['signal_jni.dll', 'libsignal_jni.dylib']
    def extraResourcesDir = 'src/otherPlatformLibraries/resources'

    doFirst {
        mkdir extraResourcesDir
        extraResources.each { name ->
            download {
                src 'https://github.com/signalapp/libsignal-client/releases/download/v' + project.version + '/' + name
                dest extraResourcesDir + '/' + name
            }
        }
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ':makeJniLibrariesDesktop'
}

// MARK: Publishing

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact testableJar

            pom {
                description = 'Signal Protocol cryptography library for Java'
                url = 'https://github.com/signalapp/libsignal-client'

                scm {
                    url = 'scm:git@github.com:signalapp/libsignal-client.git'
                    connection = 'scm:git@github.com:signalapp/libsignal-client.git'
                    developerConnection = 'scm:git@github.com:signalapp/libsignal-client.git'
                }

                licenses {
                    license {
                        name = 'AGPLv3'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Signal Messenger LLC'
                    }
                }
            }
        }
    }
    repositories {
    }
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask(":java:publish") }
    sign publishing.publications.mavenJava
}
